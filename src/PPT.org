#+TITLE: PPT.jl
#+AUTHOR: Stefanos CarlstrÃ¶m
#+EMAIL: stefanos.carlstrom@gmail.com

#+PROPERTY: header-args:julia :session *julia-PPT*

#+BEGIN_SRC julia
  using Cubature
  using Unitful
#+END_SRC

#+RESULTS:
: nothing

* Unit conversions
  #+BEGIN_SRC julia
    @derived_dimension ElectricField Unitful.ðˆ^-1*Unitful.ð‹*Unitful.ðŒ*Unitful.ð“^-3
    @derived_dimension Intensity Unitful.ðŒ*Unitful.ð“^-3

    Ï„â‚€ = 24.1888430u"as"

    atomic_units(E::ElectricField) = E/(5.14220651e11u"V"/u"m") |> NoUnits
    atomic_units(I::Intensity) = I/(3.5094452e16u"W"/u"cm"^2) |> NoUnits
    atomic_units(t::Unitful.Time) = t/Ï„â‚€ |> NoUnits
    atomic_units(Wk::Unitful.Energy) = Wk/27.211u"eV" |> NoUnits
  #+END_SRC

  #+RESULTS:
  : atomic_units (generic function with 4 methods)

* Atomic data
  #+BEGIN_SRC julia
    Iâ‚š = Dict(:argon => 15.7596/27.211, :neon => 21.5645/27.211, :helium => 24.587387/27.211)
    nstar = Dict(k => 1/âˆš(2v) for (k,v) in Iâ‚š)
    â„“ = Dict(:argon => 1, :neon => 1, :helium => 1); # Is 1 right for helium?

    check_element(element::Symbol) = element âˆˆ keys(Iâ‚š) || error("Atomic data not present for element $(element)")
  #+END_SRC

  #+RESULTS:
  : check_element (generic function with 1 method)

* Minor helper functions
  All functions work in atomic units, unless otherwise
  specified. Equation references are wrt to

  - Perelomov, A., Popov, V., & Terent'ev, M. (1966). Ionization of
    atoms in an alternating electric field. Soviet Physics---Journal of
    Experimental and Theoretical Physics, 23(5), 924â€“934,

  or, if specified,

  - Ammosov, M. V., Delone, N. B., & Krainov, V. P. (1986). Tunnel
    ionization of complex atoms and of atomic ions in alternating
    electromagnetic field. Soviet Physics---Journal of Experimental and
    Theoretical Physics, 64(), 1191â€“1194.

  #+BEGIN_SRC julia
    Î“ = Base.gamma

    # Eq. (19) of ADK
    CÂ²(n, â„“) = 2.0^(2n)/(n*Î“(n+â„“+1)*Î“(n-â„“))

    # Eq. (4)
    function f(â„“::Integer,m::Integer)
        â„“ >= 0 || error("Invalid value of â„“ ($(â„“) < 0)")
        abs(m) <= â„“ || error("Invalid value of m ($(abs(m)) > $(â„“))")
        (2â„“+1)*factorial(â„“+abs(m))/(2^abs(m)*factorial(abs(m))*factorial(â„“-abs(m)))
    end

    Uâ‚š(I,Ï‰) = I/4Ï‰^2

    keldysh(element,Uâ‚š) = âˆš(Iâ‚š[element]/2Uâ‚š)
    keldysh(element,I,Ï‰) = keldysh(element,Uâ‚š(I,Ï‰))

    # Just after Eq. (56)
    ppt_Î²(Î³) = 2Î³/sqrt(1+Î³^2)
    # Eq. (34)
    Î±(Î³) = 2asinh(Î³) - ppt_Î²(Î³)
    # Eq. (33)
    g(Î³) = 3/(2Î³)*((1+1/(2Î³^2))*asinh(Î³) - (âˆš(1+Î³^2))/(2Î³))
    # Eq. (26), Iâ‚š = ÎºÂ²/2
    Î½(element, I, Ï‰) = Iâ‚š[element]/Ï‰ * (1.0 + 1/2keldysh(element,I,Ï‰)^2)

    # Eq. (56)
    function w(m,x,eps=1e-7)
        val,err = hquadrature(t -> exp(-x^2*t)*t^abs(m)/âˆš(1-t), 0, 1-eps)
        x^(2abs(m)+1)/2*val
    end

    # Eq. (55)
    function A(m,element,I,Ï‰, tol = 1e-5)
        Î³ = keldysh(element,I,Ï‰)
        nÎ½ = Î½(element, I, Ï‰)
        S = 0
        dS = Inf
        Îº = ceil(Int, nÎ½)
        ii = 0
        Î±_Î³ = Î±(Î³)
        ppt_Î²_Î³ = ppt_Î²(Î³)
        while dS/S > tol
            dk = Îº - nÎ½
            dS = exp(-Î±_Î³*dk)*w(m,âˆš(ppt_Î²_Î³*dk))
            S += dS
            Îº += 1
            ii += 1
        end
        (4/âˆš(3Ï€))/factorial(abs(m)) * Î³^2/(1+Î³^2) * S
    end

    # Eq. (5) + (54)
    EÌƒ(Eâ‚€, Iâ‚š) = Eâ‚€/(2(2Iâ‚š)^1.5)
  #+END_SRC

  #+RESULTS:
  : áº¼ (generic function with 1 method)

* PPT
  #+BEGIN_SRC julia
    """
        ppt(element, I, Ï‰[, m=0])

    Calculate the ionization rate (in atomic units) of `element` according
    to the Perelomovâ€“Popovâ€“Terent'ev formalism over the intensity range `I`
    (in atomic units), for a photon energy `Ï‰` (in atomic units), and the
    projection `m` of the initial orbital angular momentum."""
    function ppt(element::Symbol, I::Real, Ï‰::Real, m::Integer=0)
        check_element(element)
        E = âˆš(I)
        Î³ = keldysh(element,I,Ï‰)

        # Eq. (54)
        ns = nstar[element]
        pre = âˆš(3/2pi)*CÂ²(ns,ns-1)*f(â„“[element],m)*Iâ‚š[element]
        Et = EÌƒ(E, Iâ‚š[element])

        Am = A(0, element, I, Ï‰)

        # Eq. (54)
        w = pre*Et^(-(2ns-abs(m)-3/2))*(1+Î³^2)^(-ns+abs(m)/2+3/4)*Am*exp(-g(Î³)/(3Et))
        isnan(w) ? 0 : w
    end

    """
        ppt(element, I, Ï‰[, m=0])

    Calculate the ionization rate (in SI units, Hz) of `element` according
    to the Perelomovâ€“Popovâ€“Terent'ev formalism over the intensity range `I`
    (in SI units, W/cmÂ²), for a photon energy `Ï‰` (in SI units, energy),
    and the projection `m` of the initial orbital angular momentum."""
    ppt(element::Symbol, I::Intensity, Ï‰::Unitful.Energy, m::Integer=0) =
        ppt(element, atomic_units(I), atomic_units(Ï‰), m)/Ï„â‚€ |> u"Hz"

    export ppt
  #+END_SRC

  #+RESULTS:
  : nothing

* ADK
  #+BEGIN_SRC julia
    """
        adk(element, I, Ï‰[, m=0])

    Calculate the ionization rate (in atomic units) of `element` according
    to the Ammosovâ€“Deloneâ€“KraÄ­nov formalism over the intensity range `I`
    (in atomic units), for a photon energy `Ï‰` (in atomic units), and the
    projection `m` of the initial orbital angular momentum."""
    function adk(element::Symbol, I::Real, Ï‰::Real, m::Integer=0)
        check_element(element)
        E = âˆš(I)
        # Approximately Eq. (1), ADK, needs to be checked
        ns = nstar[element]
        pre = CÂ²(ns,ns-1)*f(â„“[element],m)*Iâ‚š[element]
        Et = EÌƒ(E, Iâ‚š[element])
        w = pre*Et^(-(2ns-abs(m)-1))*exp(-1.0/(3Et))
        isnan(w) ? 0 : w
    end

    """
        adk(element, I, Ï‰[, m=0])

    Calculate the ionization rate (in SI units, Hz) of `element` according
    to the Ammosovâ€“Deloneâ€“KraÄ­nov formalism over the intensity range `I`
    (in SI units, W/cmÂ²), for a photon energy `Ï‰` (in SI units, energy),
    and the projection `m` of the initial orbital angular momentum."""
    adk(element::Symbol, I::Intensity, Ï‰::Unitful.Energy, m::Integer=0) =
        adk(element, atomic_units(I), atomic_units(Ï‰), m)/Ï„â‚€ |> u"Hz"

    export adk
  #+END_SRC

  #+RESULTS:
  : nothing

* Ionization probability
  If we have an intensity-dependent ionization rate, \(f(I)\), the
  rate equation reads

  \[\dot{q} = -f(I)q,\quad q_0=1.\]

  If the intensity is time-dependent, such as in a laser pulse, we
  integrate the rate equation over the intensity profile, assuming no
  initial ionization:

  \[q(t) =
  \exp\left\{
  -\int_{-\infty}^t\mathrm{d}t f[I(t)]
  \right\}.\]

  Since the probability of being ionized is \(\tilde{q} = 1 - q\), we
  have

  \[\tilde{q}(t) = 1 - \exp\left\{
  -\int_{-\infty}^t\mathrm{d}t f[I(t)]
  \right\}.\]

  #+BEGIN_SRC julia
    for (TimeType, IntensityType, EnergyType) in
        [(Unitful.Time, Intensity, Unitful.Energy),
         (Real,Real,Real)]
        @eval begin
            """
        ionization_probability(rate, t, I, Ï‰, element[, m])

    Calculate the time-resolved ionization probability of `element`
    subjected to a laser pulse of energy `Ï‰` and a temporal intensity
    profile `I`."""
            function ionization_probability(rate::Symbol,
                                            t::AbstractVector{<:$(TimeType)},
                                            I::AbstractVector{<:$(IntensityType)}, Ï‰::$(EnergyType),
                                            element::Symbol, m::Integer = 0)
                rate âˆ‰ [:ppt, :adk] && error("Unknown ionization rate, $rate")
                rate_fun = Dict(:ppt => ppt, :adk => adk)[rate]
                dt = (t[2]-t[1])
                1.0 - exp.(-cumsum(rate_fun.(element, I, Ï‰, m))*dt .|> NoUnits)
            end
        end
    end

    export ionization_probability
  #+END_SRC

  #+RESULTS:
  : nothing

* EXAMPLE Usage
** Ionization rate
   #+BEGIN_SRC julia
     I = logspace(12,15, 1001)*u"W/cm^2";
     Ï‰ = 1.5u"eV"

     ppt_rate = ppt.(:argon, I, Ï‰, 0) .|> u"PHz";
     adk_rate = adk.(:argon, I, Ï‰, 0) .|> u"PHz";
   #+END_SRC

   #+RESULTS:

   #+BEGIN_SRC julia :exports results :results value file
     using PyPlot
     import Jagot.plotting: no_tick_labels

     function savefig_f(filename)
         path = joinpath("..", "figures", "$(filename).svg")
         mkpath(dirname(path))
         savefig(path, transparent=true)
         path
     end

     figure("Ionization rate")
     clf()
     loglog(I./u"W/cm^2" .|> NoUnits, ppt_rate./u"PHz" .|> NoUnits, label="PPT")
     yl = ylim()
     loglog(I./u"W/cm^2" .|> NoUnits, adk_rate./u"PHz" .|> NoUnits, label="ADK")
     ylim(yl...)
     xlabel(L"Intensity [W/cm$^2$]")
     ylabel("Rate [PHz]")
     legend()
     title(L"Ionization rate of argon in by $\hbar\omega$ = 1.5 eV")
     tight_layout()
     savefig_f("ion-rate")
   #+END_SRC

   #+RESULTS:
   [[file:../figures/ion-rate.svg]]

** Ionization probability
   #+BEGIN_SRC julia
     intensity(E::ElectricField) = u"Îµ0"*u"c"/2*E^2 |> u"W"/u"cm"^2

     t = linspace(-1,1,1001)*10u"fs"
     Ï‰ = 1.5u"eV"

     Ï„ = 6u"fs"
     Ïƒ = Ï„/(2*âˆš(2log(2)))

     E = 6e10u"V/m" * exp.(-t.^2./2Ïƒ^2) .* sin.(Ï‰*t/u"Ä§");
     I = intensity.(E);
     ppt_prob = ionization_probability(:ppt, t, I, Ï‰, :argon);
     adk_prob = ionization_probability(:adk, t, I, Ï‰, :argon);
   #+END_SRC

   #+RESULTS:

   #+BEGIN_SRC julia :exports results :results value file
     figure("ionization probability")
     clf()
     subplot(311)
     plot(t./u"fs" .|> NoUnits, E./u"V/m" .|> NoUnits)
     no_tick_labels()
     ylabel(L"$E$ [V/m]")
     subplot(312)
     plot(t./u"fs" .|> NoUnits, I./u"W/cm^2" .|> NoUnits)
     no_tick_labels()
     ylabel(L"$I$ [W/cm$^2$]")
     subplot(313)
     plot(t./u"fs" .|> NoUnits, ppt_prob, label="PPT")
     plot(t./u"fs" .|> NoUnits, adk_prob, label="ADK")
     legend()
     xlabel(L"$t$ [fs]")
     ylabel("Ionization probability")
     savefig_f("ionization-probability")
   #+END_SRC

   #+RESULTS:
   [[file:../figures/ionization-probability.svg]]
